    1 &"{f6}{f6}\c3Attack Whom? {f6}\c1(0=Nobody,?=List,Y=Your Stats{f6}L=Lord Randall){f6}\cn>>\co"
    2 bn=0:gosub1006:iftr%<1then280
    4 ifan$="0"oran$=""then:&"{f6}\c2No Attack!{f6}":goto182
    5 ifan$="L"then625
    6 ifan$="{f2}"thengosub392
    8 ifan$="Y"thengosub406:goto1
   10 g1=int(val(an$)):ifg1<1org1>24then1
   12 gosub404:x=g1:gosub1002:input#2,en$
   14 ifleft$(en$,5)="empty"oren$="0"then:&"\c2Invalid #!":close2:goto1
   16 input#2,ez,ew,ef,en,eg,et,ep,e1,e2,e3,e4,e5,e6,e7,e8,og:close2
   17 ifez=.then550
   18 &"\cn{f6}The Enemy \co-->\c1"+en$+"\co<--{f6}\c1Land\co-->\cn"+str$(ez)
   20 &"\co{f6}{f6}Are you sure?\c1 ":gosub1902:ifan$<>"Y"then1
   21 ifez>200000and(la-50000)>ezthen625
   22 i=1:ifpeek(901)=.then28
   24 a=peek(901+i):on-(a=x)goto26:i=i+1:on-(i>peek(901))goto28:goto24
   26 &"{f6}\c7\s1You Already Did!!\s0\w1\co Try Someone ELSE!!!{f6}":goto1
   28 poke901,peek(901)+1:poke901+peek(901),x:goto30
   30 on-(g1<>g3)goto34:&"{f6}{f6}\cjAttack YOURSELF?{f6}\c2Bad Mistake!{f6}\co"
   32 la=la-int(rnd(1)*(la/2)):goto182
   34 ifaf=1orwa<1then:&"{f6}{f6}\c7Your Army Is Immobilized!":goto182
   35 sd=0:nj=0
   36 &"{f6}{f6}\c3Battle Command?{f6}([1-6]){f6}\cn>>\co":gosub1006
   37 iftr%<1then280
   38 cm=val(an$):ifan$=""oran$="{f2}"thengosub378:goto36
   40 oncmgoto60,110,144,164,42,124,600:goto36
   42 &"{f6}\c4Your Diplomat rides to the enemy camp...{f6}":goto56
   44 ifew<wa*2then48
   46 goto52
   48 lt=int(ez/15):la=int(la+lt):ez=int(ez-lt):&"{f6}\c7He returns with good news!"
   50 &"{f6}To avoid attack you have been given{f6}"+str$(lt)+" acres of land!{f6}"
   51 &"{f6}{f6}\coYou now have"+str$(la)+" acres.\cn":ff=1:sd=1:goto102
   52 f%(6)=f%(6)-1:&"{f6}\c8The enemy \r1\c4BEHEADS\r0 \c8your Diplomat, and"
   54 &"{f6}tosses his corpse into the moat!!{f6}":sd=0:goto36
   56 iff%(6)<1then:&"{f6}{f6}\c7You have no Diplomats!\c1{f6}{f6}":goto36
   58 goto44
   60 gosub160:onaegoto34:gosub610:a=(wa+la)/(ew+ez)
   72 &"{f6}{f6}\c1Your Army     \c7Enemy Army{f6}"
   74 ff=1:pv=.:sr=ew:sg=wa:a=0:a2=20:b2=20
   76 ifa=5thena=0:gosub700
   78 a=a+1
   80 wz=(wa*.08):ed=(ew*.08)
   82 if(rnd(1)*wz)+(rnd(1)*(300+z9*5))>(rnd(1)*ed)+(rnd(1)*(300+og*5))then90
   84 wa=wa-1:a2=a2-1:b2=20:ifwa>.anda2>.then76
   86 wa=0:pv=1:ew=int(ew+rnd(1)*(sr-ew)):goto106
   90 ew=ew-1:b2=b2-1:a2=20:ifew>.andb2>.then76
   92 bn=1:ifwa>int(rnd(1)*ez)then:&"{f6}{f6}You conquered his land!!!":lt=ez:goto100
   93 ew=0:&"{f6}{f6}\cnYour forces were victorious!{f6}{f6}Your"+str$(wa)+" remaining"
   94 &" soldiers managed to capture":z9=z9+1:ifz9>100thenz9=100
   96 lt=abs(int(wa*.7+200+rnd(1)*300+rnd(1)*200+ez*.01)):iflt>ezthenlt=ez
   98 &str$(lt)+" acres...":ifwathenew=int(en*20/2+rnd(1)*(en*20/2))
   99 ifbn=1thengosub705:bn=.
  100 ez=int(ez-lt):la=int(la+lt):&"{f6}{f6}\coYou now have"+str$(la)+" acres.\cn"
  102 ifez<1then:&"{f6}{f6}\cnYou also killed "+en$+"!{f6}":w$="empty"+chr$(13)+"0":goto170
  104 &"{f6}{f6}"+en$+" has"+str$(ez)+" acres left.":ew=int(ew):goto164
  106 bn=.:&"{f6}{f6}\cnYour forces were defeated!{f6}{f6}\coBut you managed to seize"
  107 z9=z9-1:ifz9<-25thenz9=-25
  108 lt=abs(sr-ew):lt=abs(int(lt*rnd(1)*4)+100):ifen$="RANDALL"thenlt=10
  109 goto98
  110 &"{f6}{f6}You attack an enemy Palace...\co":gosub160:ifaethen34
  112 ife1<1then:&"{f6}{f6}They have no palaces!!!":goto36
  114 ifrnd(1)*wa<rnd(1)*ew*3then156
  116 &"{f6}You destroyed one of their Palace!!!{f6}":sl=int(rnd(1)*(wa*.5))+1
  118 e1=e1+(e1>.):en=en+(en>.):ifew>en*20thenew=en*20
  120 &"{f6}\cn-->You lost"+str$(sl)+" of your soldiers in the process...\co"
  122 wa=wa-sl:goto34
  124 &"{f6}{f6}\coYour Noble mounts his mighty steed{f6}and takes his lance...{f6}"
  125 goto140
  126 ifen*2<nbthen132
  128 ifen<2then:&"{f6}Your opponent has no nobles.{f6}":goto34
  130 goto136
  132 nb=nb+1:en=en-1:&"{f6}\cnYour Noble's lance knocks his opponent{f6}to the"
  134 &" ground. He gets up and swears{f6}loyalty to you!{f6}":ff=1:nj=1:goto164
  136 nb=nb-1:en=en+1:&"{f6}\coYour opponent's lance pierces armor.{f6}Your noble,"
  138 &" badly injured, joins your{f6}opponent's army!{f6}":nj=0:goto36
  140 ifsf<900then:&"{f6}{f6}{lblu}Not enough serfs attend.{f6}The Joust is cancelled!{f6}{f6}":goto34
  142 goto126
  144 &"{f6}{f6}\coYou sneak up on a noble...":gosub160:ifaethen34
  146 ifrnd(1)*wa<rnd(1)*ew*1.7then156
  148 ifen<2then:&"{f6}{f6}You can't find any nobles!":goto36
  150 ifnb>int(la/1000)then:&"{f6}{f6}\c7Your Noble's threaten to strike!{f6}":goto36
  152 &"{f6}\cnYou punch him out and drag his twitching carcass off!!!{f6}"
  154 nb=nb+1:en=en-1:sl=int(rnd(1)*(wa*.4))+1:goto120
  156 &"{f6}\coGuards appear and halt your attempts!{f6}":sl=int(rnd(1)*(wa*.3))+2
  158 &"{f6}{f6}\c1-->\coThe Guards kill"+str$(sl)+" of your men!{f6}":goto122
  160 ae=.:ifwa<1then:&"{f6}{f6}\c2You have no soldiers!\cn":ae=1:wa=.
  162 return
  164 r$=chr$(13):w$=en$+r$+str$(ez)+r$+str$(ew)+r$+str$(ef)+r$+str$(en)
  166 w$=w$+r$+str$(eg)+r$+str$(et)+r$+str$(ep)+r$+str$(e1)+r$+str$(e2)+r$
  168 w$=w$+str$(e3)+r$+str$(e4)+r$+str$(e5)+r$+str$(e6)+r$+str$(e7)+r$+str$(e8)
  169 w$=w$+r$+str$(og)
  170 on-(z8=1)goto180:gosub404:x=g1:gosub1002:print#2,w$:close2
  172 ifez<1thengr=gr+eg:pn=pn+ep:sf=sf+ef:f%(4)=f%(4)+e4:f%(3)=f%(3)+e3:goto176
  174 goto180
  176 &"{f6}\coYour army seizes"+str$(eg)+" bushels of{f6}grain and"+str$(ep)+" doll"
  178 &"ars.{f6}"+en$+"'s"+str$(ef)+" serfs declare{f6}loyalty to you.\w2{f6}"
  180 gosub430
  182 ifff=0then196
  184 &"{f6}{f6}\c1Spreading the news...\co{f6}"
  185 gosub474
  186 x=1:tt$="\cl"+d2$+"\co"+na$+"\c1 defeats \co"+en$
  187 ifpv=1thentt$="\co"+en$+"\c1 resists \cl"+d2$+"\co"+na$
  188 ifez<1thentt$="\cl"+d2$+"\co"+na$+"\c1 executes \co"+en$
  190 gosub468:a$="":gosub1029:&tt$
  192 b$="empnews5":a$="a":dr=3
  193 gosub1027:gosub1012:a$="":ife%>19then442
  194 print#2,"\cn\vz\c1{CTRL-D}"+d1$:print#2,tt$:close2
  196 gosub800:gosub320:goto226
  226 ifla<1then:&"{f6}{f6}\cnYou've lost all your land!{f6}":goto234
  227 &"{f6}{f6}\r1PRESS ANY KEY\r0\g1"
  228 ifsf>99then236
  230 &"{f6}{f6}\cnYou haven't enough serfs to maintain{f6}the Empire! It's turned over"
  232 &" to King{f6}George and you are \c2beheaded\cn.{f6}"
  234 w$="empty"+chr$(13)+"0":gosub436:goto284
  236 p2=int(((rnd(1)*75)+sf/100)*f%(2)):p3=int(((rnd(1)*100)+gr/1000)*f%(3))
  238 p4=((rnd(1)*175)+wa)*f%(4)/(2-ff):p5=((rnd(1)*200)+la/30)*f%(5)/(2-ff)
  240 p4=int(p4-(p4*tr/200)):p5=int(p5-(p5*tr/150))
  242 &"{f6}{f6}\r1\co->Profit & Loss Report<-{f6}{f6}\c1"
  244 py=(wa*(ff+2))+(tr*f%(1)*10)/40:x=int(tr*(rnd(0)*nb))*100/4
  246 pn=int(pn+p2+p3+p4+p5):tg=int((p2+p3+p4+p5)*tr/100):pn=pn+tg
  247 gosub500
  248 ifpn>py+xthenpn=pn-py-x:goto251
  250 py=0:x=0
  251 &"{f6}{f6}\cnRECEIVABLES\c1"
  252 &"{f6}{f6} Markets:  $"+mid$(str$(p2),2)+"{f6} Mills:    "+str$(p3)+"{f6} Foundries:"
  254 &str$(p4)+"{f6} Shipyards:"+str$(p5)+"{f6} Taxes:    "+str$(tg):lp=1:gosub426
  256 &"{f6}\cnPAYABLES\c1{f6}{f6} Soldiers Pay:"+str$(wa*(ff+2))+"{f6} Palace Rent :"
  258 &str$(tr*f%(1)*10)+"{f6} Nobles Gifts:"+str$(x)
  260 lp=1:gosub426:gosub298
  261 gosub406:ifwa=nb*20then280
  262 x=int(rnd(1)*19)+9:b$=str$(nb*20-wa):an$=str$(x)
  264 &"{f6}{f6}\c3You can buy up to\v6 more{f6}soldiers to protect your Empire."
  266 &"{f6}These soldiers cost\v7 dollars each."
  268 &"{f6}\c1You have"+str$(pn)+" dollars.{f6}"
  270 &"{f6}{f6}\c3Buy how many? \co":gosub1006:iftr%<1then280
  272 b=int(val(an$)):on-(b<1)-(b*x>pn)*2goto280,270
  274 ifwa+b>(f%(4)+1)*100thengosub448:goto270
  276 ifwa+b>nb*20then:&"{f6}You only have"+str$(nb)+" nobles!{f6}":goto270
  278 wa=int(wa+b):pn=int(pn-(b*x))
  280 poke899,peek(899)+1:&"{f6}{f6}\coFixing up the Palace, please wait...."
  282 gosub430:iftr%<1then1811
  284 &"{f6}{f6}\c3Play Empire Again? \co":gosub1902:iftr%<1then1811
  286 on-(a=.)goto292:ifpeek(899)<5ore9=1then454
  288 &"{f6}\c1Haven't you had enough yet?\co ":gosub1902:ifa=1then292
  290 &"{f6}\c8Tough Bananas \c4\v2 !!!!!{f6}{f6}\c1Wait'll The Sysop Hears"
  291 &" About This!{f6}{f6}":goto294
  292 gosub474:&"{f6}\cnGoodbye, \cl"+d2$+"\co"+na$+"...{f6}\c3Play again soon!!!{f6}{f6}"
  294 close2:close15:&,28:goto1812
  296 return
  298 tz=0
  300 i1=f%(1):i2=f%(2):i3=f%(3):i4=f%(4):i5=f%(5):i6=f%(6)
  302 ifi2>9andi6>0andi3>5andi4>1andi5>1andi1>2and(la/sf>5.1)andnb>15andsf>3000thengosub482
  304 ifi2>15andi3>9andi6>2andi4>6andi5>4andi1>6and(la/sf>10.5)andsf>3500andnb>30thengosub484
  305 gosub555:ifmp=0andla>lnthengosub486
  306 ifi2>23andi3>9andi4>13andi5>11andi6>2andi1>9and(la/sf>23.4)andsf>5499andnb>62thengosub486
  307 iftz=tythen310
  308 iftz>0thenb$="empnews5":a$="a":dr=3:goto314
  310 ty=tz:return
  314 iftz>tythentt$="\co"+na$+"\c1 has been crowned \cl"+d2$:goto317
  316 iftz<tythentt$="\co"+na$+"\c1 has been demoted to \cl"+d2$
  317 gosub1027:gosub1012:a$="":ife%>19then440
  318 print#2,"\cn\vz\c1{CTRL-D}"+d1$:print#2,tt$:close2:ty=tz:a$=tt$+"\b1\w2":goto1029
  320 ifpy=0andwa>500thena=int(wa/5):wa=wa-a:gosub450
  322 ifx=0andnb>67thena=int(nb/5):nb=nb-a:gosub452
  324 iff%(5)<400then332
  326 v=int(f%(5)/1.1):f%(5)=f%(5)-v
  328 &"{f6}\cnYour Kingdom cannot support this many{f6}shipyards!  "+str$(v)+"\c4"
  330 &" are CLOSED.{f6}"
  332 ifpn<1000000then340
  334 f=int(pn/1.5):pn=pn-f
  336 &"{f6}\cnYou Pay"+str$(f)+" \coDollars to the Monks,{f6}for this years "
  338 &"Provisions for your{f6}Subjects' survival.\co{f6}"
  340 ifla<2500000then346
  342 c=int(la/2.5):la=la-c
  344 &"{f6}\c2You Donate"+str$(c)+" \c7Acres of Land{f6}for the Monestary.\co{f6}"
  346 iff%(4)<400then354
  348 j=int(f%(4)/3):f%(4)=f%(4)-j
  350 &"{f6}{f6}\r1\cnMAJOR EXPLOSION!!!\r0{f6}"+str$(j)+"\c6 Foundries are "
  352 &"\p4Destroyed!\p0{f6}{f6}"
  354 iff%(2)<500then360
  356 k=int(f%(2)/5):f%(2)=f%(2)-k
  358 &"{f6}{f6}\c8Some of the owners retire"+str$(k)+"\c4 Markets close.{f6}{f6}"
  360 iff%(3)<500then368
  362 r=int(f%(3)/4):f%(3)=f%(3)-r
  364 &"{f6}{f6}\c5The Mills are Overworked!  "+str$(r)+"\c1 Mills{f6}have broken "
  366 &"millstones and are shutdown.\co{f6}{f6}"
  368 goto456
  378 &"\f1\c2\n2Fight Menu{f6}\c1{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{f6}{f6}"
  380 &"{f6}\c3{rvon}[1]{rvof} \c7Attack Army{f6}"
  382 &"\c3{rvon}[2]{rvof} \c7Attack Palace{f6}"
  384 &"\c3{rvon}[3]{rvof} \c7Attack Nobles{f6}"
  386 &"\c3{rvon}[4]{rvof} \c7Cease Fighting{f6}"
  388 &"\c3{rvon}[5]{rvof} \c7Send Diplomat{f6}":&"\c3{rvon}[6]{rvof} \c7Joust{f6}"
  389 a$="\c3{rvon}[7]{rvof} \c7Donate To Him{f6}":goto1029
  392 sh=.:&"{f6}{f6}\c2(Space=Stop){f6}{f6}\c7OTHER{$a0}RULERS{f6}\c1\vz\co":gosub404:x=1
  394 gosub1002:input#2,an$:ifleft$(an$,5)="empty"oran$=na$oren$="0"then400
  396 gosub1002:input#2,an$,b:b$=right$(" "+str$(x),2)
  398 an$=left$(an$+"...............",20):&"{f6}\co\v6> \cn\v7\c3:"+str$(b)
  400 ifrcorsh=47then:&"{f6}\c2(Aborted){f6}":x=25
  402 x=x+1:on-(x<25)goto394:close2:return
  404 a$="e.empire5":dr=3:goto1011
  406 b$=str$(g3):an$=str$(la)
  408 &"{f6}{f6}\c3\v2 (#\v6){f6}{f6}\cnLand     : \v7 Acres{f6}Money    :$"+str$(pn)
  410 b$=str$(gr):an$=str$(tr)
  412 &"{f6}Grain    : \v6 Bushels{f6}Tax Rate : \v7%{f6}Soldiers : "+str$(wa)
  414 b$=str$(sf):an$=str$(nb)
  416 &"{f6}Serfs    : \v6{f6}Nobles   : \v7":gosub426
  418 b$=str$(f%(1)):an$=str$(f%(2))
  420 &"\c3Palaces  : \v6{f6}Markets  : \v7{f6}Mills    : "+str$(f%(3))
  422 b$=str$(f%(4)):an$=str$(f%(5))
  424 &"{f6}Foundries: \v6{f6}Shipyards: \v7{f6}Diplomats: "+str$(f%(6))
  425 &"{f6}Ships    : "+str$(yc)+"{f6}Colonys  : "+str$(i8)+"{f6}Training : "+str$(z9)
  426 &"{f6}{f6}\c1\r1 (Press A Key) \r0{f6}\g1{f6}"
  428 return
  430 r$=chr$(13):w$=na$+r$+str$(la)+r$+str$(wa)+r$+str$(sf)+r$+str$(nb)
  432 w$=w$+r$+str$(gr)+r$+str$(tr)+r$+str$(pn)+r$+str$(f%(1))+r$+str$(f%(2))
  434 w$=w$+r$+str$(f%(3))+r$+str$(f%(4))+r$+str$(f%(5))+r$+str$(f%(6))
  435 w$=w$+r$+str$(yc)+r$+str$(i8)+r$+str$(z9)
  436 gosub404:x=g3:gosub1002:print#2,w$:close2:return
  440 b$="empnews5":a$="w":dr=3:gosub1027:print#2,"\cn As Of: {CTRL-D}"+d1$+"{f6}":close2:goto298
  442 b$="empnews5":a$="w":dr=3:gosub1027:print#2,"\cn As Of: {CTRL-D}"+d1$+"{f6}":close2:goto185
  444 return
  448 &"{f6}You only have"+str$(f%(4))+" foundries!{f6}":goto1029
  450 &"{f6}"+str$(a)+"\co soldiers desert your army.{f6}":goto1029
  452 &"{f6}"+str$(a)+"\co of your nobles defect.{f6}":goto1029
  454 z8=2:dr=5:a$="Empire 5.1":goto1067
  456 ifwa<((nb*20)+1)thenreturn
  458 wb=int(wa-nb*20):wa=wa-wb
  459 &"{f6}{f6}\c3Your Nobles are too few to support{f6}your army! Your excess soldiers"
  460 &" are{f6}taken by King George.{f6}\w2":goto1029
  468 ifnj=1thentt$="\cl"+d2$+"\co"+na$+"\c1 Wins Joust \co"+en$+"\c1 is shamed."
  470 ifsd=1thentt$="\co"+en$+"\c1 Forfeits \cl"+d2$+"\co"+na$+"'s \c1Attack."
  472 return
  474 d2$="LORD ":iftz=1thend2$="PRINCE "
  476 iftz=2thend2$="KING "
  478 iftz=3thend2$="EMPEROR "
  480 return
  482 d2$="PRINCE ":tz=1:return
  484 d2$="KING ":tz=2:return
  486 ifla<lnthenreturn
  487 d2$="EMPEROR ":tz=3
  488 return
  500 pt=int(p2+p3+p4+p5+tg)
  501 &"{f6}\cnTOTALS\c1{f6} Receivables = "+str$(pt)+"{f6}"
  502 &" Payables    = "+str$(py+x)+"{f6}"
  503 ifpt>py+xthen:&"{f6} Profit      = "+str$(pt-py-x):pt=0:gosub426:return
  504 ifpt<py+xthen:&"{f6} Loss        = "+str$(py+x-pt)
  505 ifpt<py+xthenpn=pn-(py+x-pt)
  506 pt=0:goto426
  550 ifez=.then:&"{f6}{wht}"+en$+" Has No Land{f6}":goto1
  551 ifew=.then:&"{f6}{wht}"+en$+" Has No Soldiers{f6}":goto1
  555 a$="e.emperor,s,r":dr=3:gosub1011
  557 input#2,ln$:input#2,mp$
  558 ln=val(ln$):mp=val(mp$)
  560 close15:close2:return
  600 rem donate
  602 &"{clr}{f6}{f6}\c1\r1<<< Cyclone's Natural Disaster Bank >>>\r0\q0{f6}{f6}"
  603 &"Money--> "+str$(pn)+"{f6}{f6}"
  605 &"{f6}Give Him How Much Money--> ":gosub1006:on-(tr%<1)goto1811:ifval(an$)then607
  606 goto36
  607 ifval(an$)>pnorval(an$)<1then:&"{f6}{f6}Get Lost!":goto36
  608 pn=pn-int(val(an$)):ep=ep+int(val(an$)):&"{f6}{f6}Done!!!{f6}{f6}":goto164
  610 &"{clr}{f6}{wht}  Naval Battle{f6}{f6}     Your Navy    Opponent Navy{f6}{f6}"
  612 ifyc<1ore7<1thengoto622
  614 a=int(rnd(1)*100)+1:ifa>55thengoto618
  616 e7=e7-1:goto620
  618 yc=yc-1
  620 &"\_09"+str$(yc)+"\_24"+str$(e7)+"{f6}":goto612
  622 on-(yc<1)goto624:ew=ew-(yc*int(rnd(1)*7)):return
  624 wa=wa-(e7*int(rnd(1)*7)):return
  625 &"{clr}{pur}{f6}{f6}Lord Randall is furious at you!{f6}Watch out!{f6}{f6}"
  627 z8=1:ez=int(rnd(1)*50000)+100000:ew=int(rnd(1)*(wa/2))+wa:en=100
  628 en$="RANDALL":ef=10000:e1=10:e2=1000:e3=1000:e4=1000:e5=1000:og=30
  629 e7=int(rnd(1)*yc/2)+yc
  630 goto60
  700 &"\c1\_03"+str$(wa)+"\_17"+str$(ew)
  701 &"{f6}":return
  705 &"{f6}{f6}On Your New Land You Find:"
  707 a=int(rnd(1)*(e5/6)):&"{f6}{f6}Shipyards: "+str$(a):f%(5)=f%(5)+a:e5=e5-a
  709 a=int(rnd(1)*(e4/6)):&"{f6}Foundries: "+str$(a):f%(4)=f%(4)+a:e4=e4-a
  710 a=int(rnd(1)*(e3/6)):&"{f6}Mills    : "+str$(a):f%(3)=f%(3)+a:e3=e3-a
  712 a=int(rnd(1)*(e2/6)):&"{f6}Markets  : "+str$(a):f%(2)=f%(2)+a:e2=e2-a
  714 a=int(rnd(1)*(ef/10)):&"{f6}Serfs    : "+str$(a):sf=sf+a:ef=ef-a
  716 return
  800 if(f%(1)*100000)+100000>lathenreturn
  802 &"{f6}{f6}{wht}You don't have enough palaces for your land!!!{f6}{f6}"
  804 la=f%(1)*100000+int(rnd(1)*100000):return
  999 rem
